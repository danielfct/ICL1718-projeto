options {
  STATIC=false;
}

PARSER_BEGIN(Parser)

package parser;
import java.util.*;
import ast.*;
import ast.ASTFun.Parameter;
import types.*;


public class Parser {

}

PARSER_END(Parser) 

SKIP :
{
  	" "
	|
	"\t"
	|
	"\r"
	|
	"\n"
}

TOKEN :
{
  	< NUM : (["0"-"9"]) + >
  	|
  	< PLUS : "+" >
  	|
  	< MINUS : "-">
  	|
  	< TIMES : "*">
  	|
  	< DIV : "/">
  	|
  	< LPAR : "(" >
  	|
  	< RPAR : ")" >
  	|
  	< TRUE : "true" >
  	|
  	< FALSE : "false" >
  	|
  	< OR : "||" >
  	|
  	< AND : "&&" >
  	|
  	< COMPARE : "==" >
 	|
 	< NEQ : "!=" >
 	|
 	< LEQ : "<=" >
  	|
  	< GEQ : ">=" >
  	|
  	< LT : "<" >
  	|
  	< GT : ">" >
  	|
  	< NOT : "!" >
  	|
	< DECL : "decl" >
	|
	< EQ : "=" >
	|
	< IN : "in" >
	|
	< END : "end" >
	|
	< EL : ";;" >
	|
	< SEMICOLON : ";" >
	|
	< VAR : "var" >
	|
	< ASSIGN : ":=" >
	|
	< WHILE : "while" >
	|
	< DO : "do" >
	|
	< IF : "if" >
	|
	< THEN : "then" >
	|
	< ELSE : "else" >
	|
	< COMMA : "," >
	|
	< ARROW : "->" >
	|
	< FUN : "fun" >
	|
	< COLON : ":" >
 	|
 	< INT : "int" >
	|
	< BOOL : "bool" >
	|
	< REF : "ref" >
	|
	< FUNT : "funt" >
	|
	< LBRACKETS : "[" >
	|
	< RBRACKETS : "]" >
	|
	< HD : "hd" >
	|
	< TAIL : "tail" >
	|
	< FOREACH : "foreach" >
	|
  	< ID : ["a"-"z","A"-"Z"] (["a"-"z","A"-"Z","0"-"9"])* >
}

IType Type():
{ IType t; List<IType> types = new LinkedList<IType>(); }
{
    <INT>
    { return IntType.singleton; }
    | 
    <BOOL>
    { return BoolType.singleton; }
    | 
    <REF> <LPAR> t = Type() <RPAR>
    { return new RefType(t); }
    | 
    <FUNT> <LPAR> t = Type() { types.add(t); }
    			(
      				<COMMA> t = Type() { types.add(t); }
      			)*
      		<RPAR>
    { final int size = types.size(); return new FunType(types.subList(0, size-1), types.get(size-1)); }
}

IASTNode Start() :
{ IASTNode e; }
{
	e = Seq() <EL>  { return e; }
}

IASTNode Seq():
{ IASTNode e1, e2; }
{
	e1 = Assign()
  	[
  		<SEMICOLON > e2 = Seq() { e1 = new ASTSeq(e1, e2); } 
 	]
  	{ return e1; }
}

IASTNode Assign() :
{ IASTNode e1, e2; }
{
	e1 = Disjunction()
 	[
  		<ASSIGN > e2 = Assign() { e1 = new ASTAssign(e1, e2); } 
  	]
  	{ return e1; }
}

IASTNode Disjunction() :
{ IASTNode e1, e2; }
{
	e1 = Conjunction()
	(
	  	<OR> e2 = Conjunction() { e1 = new ASTOr(e1, e2); }
	)*
	{  return e1; }
}

IASTNode Conjunction() :
{  IASTNode e1, e2; }
{
	e1 = BooleanExp()
	(
	  	<AND> e2 = BooleanExp() { e1 = new ASTAnd(e1, e2); }
	)*
	{ return e1; }
}

IASTNode BooleanExp() :
{ IASTNode e1, e2; }
{
	e1 = ArithmeticExp()
	(
		<COMPARE> e2 = ArithmeticExp() { e1 = new ASTEqual(e1, e2); }
	  	|
	  	<NEQ> e2 = ArithmeticExp() { e1 = new ASTNotEqual(e1, e2); }
	  	|
	  	<LT> e2 = ArithmeticExp() { e1 = new ASTLesser(e1, e2); }
	  	|
	  	<GT> e2 = ArithmeticExp() { e1 = new ASTGreater(e1, e2); }
	  	|
	  	<LEQ> e2 = ArithmeticExp() { e1 = new ASTLesserEq(e1, e2); }
	  	|
	  	<GEQ> e2 = ArithmeticExp() { e1 = new ASTGreaterEq(e1, e2); }
	)*
	{ return e1; }
}

IASTNode ArithmeticExp() :
{ IASTNode e1, e2; }
{
   	e1 = Term()
	(
	  	<PLUS> e2 = Term() { e1 = new ASTAdd(e1,e2); }
		|
		<MINUS> e2 = Term() { e1 = new ASTSub(e1,e2); }
    )*
    { return e1; }
}

IASTNode Term() :
{ IASTNode e1, e2; }
{
     e1 = Unary()
     (
     	<TIMES> e2 = Unary() { e1 = new ASTMul(e1, e2); }
     	|
     	<DIV> e2 = Unary() { e1 = new ASTDiv(e1, e2); }
     )*
     { return e1; }
}


IASTNode Unary() :
{ IASTNode e; }
{
  	e = Call() 	{ return e; }
	|
  	<MINUS> e = Unary() { return new ASTSub(new ASTNum(0), e); }
	|
	<NOT> e = Unary() { return new ASTNot(e); }
	|
	<TIMES> e = Unary() { return new ASTDeref(e); }
}


IASTNode Call() :
{ IASTNode e; List<IASTNode> args; }
{
  	e = Fact()
  	(
    	<LPAR> args = Arguments() <RPAR> { e = new ASTCall(e, args); }  	)*
  	{ return e; }
}

List<IASTNode> Arguments() :
{ 	List<IASTNode> args = new LinkedList<IASTNode>(); IASTNode e; }
{
	[
  		e = Seq() { args.add(e); }
  		(
  	  		<COMMA> e = Seq() { args.add(e); }
  		)*
 	]
  	{ return args; }
}

IASTNode Fact() :
{ Token x; ASTDecl d; IASTNode e1, e2, e3; List<Parameter> params; IASTNode list; }
{
 	x = <ID> { return new ASTId(x.image); }
	|
	<DECL> { d = new ASTDecl(); }
		(
	  		x = <ID> <EQ> e1 = Seq() { d.newBinding(x.image, e1); }
		)+
		<IN> e2 = Seq() <END> { d.setBody(e2); return d; }
	|
   	<LPAR> e1 = Seq() <RPAR> { return e1; }
 	|
	<VAR> <LPAR> e1 = Seq() <RPAR> { return new ASTVar(e1); }
	|
	<WHILE> e1 = Seq() <DO> e2 = Seq() <END> { return new ASTWhile(e1, e2); }
	|
	<IF> e1 = Seq() <THEN> e2 = Seq() <ELSE> e3 = Seq() <END> { return new ASTIfThenElse(e1, e2, e3); }
	|
	<FUN> params = Parameters() <ARROW> e1 = Seq() <END> { return new ASTFun(params, e1); }
	|
	<LBRACKETS> list = List() <RBRACKETS> { return list; }
	|
	<FOREACH> x = <ID> <IN> <LBRACKETS> list = List() <RBRACKETS> <DO> e1 = Seq() <END> { return new ASTForEach(x.image, list, e1); }
	|
	<TRUE> { return new ASTBool(true); }
	|
	<FALSE> { return new ASTBool(false); }
	|
	x = <NUM> { return new ASTNum(Integer.parseInt(x.image)); }
}

List<Parameter> Parameters() :
{ List<Parameter> params = new LinkedList<Parameter>(); Token x; IType t; }
{
 	[
  		x = <ID> <COLON> t = Type() { params.add(new Parameter(x.image, t)); }
  		(
  	  		<COMMA> x = <ID> <COLON> t = Type() { params.add(new Parameter(x.image, t)); }
  		)*
  	]
  	{ return params; }
}

IASTNode List() :
{ IASTNode hd = null; IASTNode t; List<IASTNode> tail = new LinkedList<IASTNode>(); }
{
	[
  		hd = Seq()
  		(
  	  		<COMMA> t = Seq() { tail.add(t); }
  		)*
 	]
  	{ return new ASTList(hd, tail); }
}

